plugins {
    id 'java'
    id 'net.minecraftforge.gradle' version '5.1.+'
}

group = property('mod_group')
version = property('mod_version')
archivesBaseName = property('mod_id')

java {
    toolchain.languageVersion = JavaLanguageVersion.of(17)
}

minecraft {
    mappings channel: 'official', version: '1.20.1'

    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    mavenCentral()
    maven { url = "https://maven.minecraftforge.net" }
    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:1.20.1-47.3.0"
    implementation fg.deobf("curse.maven:create-328085:6247663")
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.named('processResources', ProcessResources).configure {
    var replaceProperties = [
            minecraft_version   : "1.20.1",
            minecraft_version_range: "[1.20.1]",
            forge_version       : "47.3.0",
            forge_version_range : "[47,)",
            loader_version_range: "[47,)",
            mod_id              : project.property('mod_id'),
            mod_name            : "Create: Structures Overhaul",
            mod_license         : "MIT License",
            mod_version         : project.property('mod_version'),
            mod_authors         : "Denmoth",
            mod_description     : "Overhauls exploration by adding immersive, Create-themed structures to the world."
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand replaceProperties + [project: project]
    }
}

tasks.named('jar') {
    manifest {
        attributes([
                "Specification-Title"     : "Create Structures Overhaul",
                "Specification-Vendor"    : "Denmoth",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "Denmoth",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }

    finalizedBy 'reobfJar'
}

tasks.register('runInPrism', Exec) {
    dependsOn tasks.jar
    group = "execution"

    def prismExe = project.hasProperty('global_prism_exe') ? project.property('global_prism_exe') : null
    def instancesDir = project.hasProperty('global_prism_instances_path') ? project.property('global_prism_instances_path') : null
    def instanceName = project.hasProperty('prism_instance_name') ? project.property('prism_instance_name') : null
    def targetModId = project.hasProperty('mod_id') ? project.property('mod_id') : "examplemod"

    doFirst {
        if (!prismExe || !instancesDir || !instanceName) {
            throw new GradleException("Missing properties! Check global .gradle/gradle.properties")
        }
        def modsDir = file("${instancesDir}/${instanceName}/minecraft/mods")
        if (!modsDir.exists()) throw new GradleException("Mods directory not found: ${modsDir}")

        fileTree(dir: modsDir, include: "${targetModId}-*.jar").each { delete it }
        copy { from tasks.jar.archiveFile; into modsDir }
    }
    commandLine prismExe, "--launch", instanceName
}